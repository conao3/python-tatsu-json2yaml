@@grammar :: JSON
@@whitespace :: //
@@parseinfo :: True

start =
    jsonvalue $
    ;

jsonobject =
    '{' ','.{ ws jsonstring ws ':' jsonvalue }* '}'
    ;

jsonarray =
    '[' ','.{ jsonvalue }* ']'
    ;

jsonvalue = ws jsonvalue_ ws;

jsonvalue_ =
    | jsonfalse
    | jsontrue
    | jsonnull
    | jsonobject
    | jsonarray
    | jsonnumber
    | jsonstring
    ;

jsonnumber = jsonfloat | jsonint;
jsonint = /[0-9]+/ ;
jsonfloat = /[0-9]+\.[0-9]+/ ;
jsonstring = '"' /(\\.|[^"])*/ '"' ;
jsonsymbol = jsontrue | jsonfalse | jsonnull ;
jsontrue = "true" ;
jsonfalse = "false" ;
jsonnull = "null" ;
ws = /[ \t\n\r]*/ ;
